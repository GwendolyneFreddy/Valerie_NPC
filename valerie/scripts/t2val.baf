// ------------------------------
// Friendship start
// ------------------------------

IF
    InParty(Myself)
    !Dead("t2val")
    Global("T2ValFriendship","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("T2ValFriendship","GLOBAL",1)
END

// Check if Mulahey is dead
IF
    InParty(Myself)
    !Dead("t2val")
    Dead("mulahey")
    Global("T2ValMulaheyAlreadyDead","GLOBAL",0)
    Global("T2ValMulaheyCheck","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("T2ValMulaheyAlreadyDead","GLOBAL",1)
    SetGlobal("T2ValMulaheyCheck","GLOBAL",1)
END

IF
    InParty(Myself)
    !Dead("t2val")
    !Dead("mulahey")
    Global("T2ValMulaheyCheck","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("T2ValMulaheyCheck","GLOBAL",1)
END

// Start friendship
IF
    InParty(Myself)
    !Dead("t2val")
    Global("T2ValMulaheyAlreadyDead","GLOBAL",0)
    Global("T2ValFriendTalk","GLOBAL",0)
    Dead("mulahey")
    AreaType([OUTDOOR])
    !Global("T2ValFriendship","GLOBAL",3)
THEN
  RESPONSE #100
    SetGlobal("T2ValFriendTalk","GLOBAL",1)
    RealSetGlobalTimer("T2ValFriendTalkTimer","GLOBAL",10)
END

IF
    InParty(Myself)
    !Dead("t2val")
    Dead("mulahey")
    Global("T2ValMulaheyAlreadyDead","GLOBAL",1)
    Global("T2ValFriendTalk","GLOBAL",0)
    !Global("T2ValFriendship","GLOBAL",3)
THEN
  RESPONSE #100
    SetGlobal("T2ValFriendTalk","GLOBAL",3)
    RealSetGlobalTimer("T2ValFriendTalkTimer","GLOBAL",2700)
END


// Triggering talks
IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("T2ValFriendTalkTimer","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
    !Global("T2ValFriendship","GLOBAL",3)
    OR(14)
      Global("T2ValFriendTalk","GLOBAL",2)
      Global("T2ValFriendTalk","GLOBAL",6)
      Global("T2ValFriendTalk","GLOBAL",8)
      Global("T2ValFriendTalk","GLOBAL",10)
      Global("T2ValFriendTalk","GLOBAL",12)
      Global("T2ValFriendTalk","GLOBAL",14)
      Global("T2ValFriendTalk","GLOBAL",16)
      Global("T2ValFriendTalk","GLOBAL",18)
      Global("T2ValFriendTalk","GLOBAL",20)
      Global("T2ValFriendTalk","GLOBAL",24)
      Global("T2ValFriendTalk","GLOBAL",28)
      Global("T2ValFriendTalk","GLOBAL",30)
      Global("T2ValFriendTalk","GLOBAL",32)
      Global("T2ValFriendTalk","GLOBAL",34)
THEN
  RESPONSE #100
    MoveToObject(Player1)
    Dialogue(Player1)
END

IF
    InParty(Myself)
    See(Player1)
    RealGlobalTimerExpired("T2ValFriendTalkTimer","GLOBAL")
    !Global("T2ValFriendship","GLOBAL",3)
    OR(11)
      Global("T2ValFriendTalk","GLOBAL",1)
      Global("T2ValFriendTalk","GLOBAL",5)
      Global("T2ValFriendTalk","GLOBAL",7)
      Global("T2ValFriendTalk","GLOBAL",9)
      Global("T2ValFriendTalk","GLOBAL",13)
      Global("T2ValFriendTalk","GLOBAL",15)
      Global("T2ValFriendTalk","GLOBAL",17)
      Global("T2ValFriendTalk","GLOBAL",23)
      Global("T2ValFriendTalk","GLOBAL",27)
      Global("T2ValFriendTalk","GLOBAL",31)
      Global("T2ValFriendTalk","GLOBAL",33)
THEN
  RESPONSE #100
    IncrementGlobal("T2ValFriendTalk","GLOBAL",1)
END


// FT 6: after bandit camp
IF
    InParty(Myself)
    !Dead("t2val")
    GlobalGT("Chapter","GLOBAL",%tutu_chapter_3%)
    Global("T2ValFriendTalk","GLOBAL",11)
    !Global("T2ValFriendship","GLOBAL",3)
    Global("T2ValBandits","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("T2ValBandits","GLOBAL",1)
    RealSetGlobalTimer("T2ValFriendTalkTimer","GLOBAL",2700)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("T2ValFriendTalkTimer","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
    !Global("T2ValFriendship","GLOBAL",3)
    Global("T2ValFriendTalk","GLOBAL",11)
    Global("T2ValBandits","GLOBAL",1)
THEN
  RESPONSE #100
    IncrementGlobal("T2ValFriendTalk","GLOBAL",1)
END

// FT 10: After clearing cloakwood
IF
    InParty(Myself)
    !Dead("t2val")
    GlobalGT("Chapter","GLOBAL",%tutu_chapter_4%)
    Global("T2ValFriendTalk","GLOBAL",19)
    !Global("T2ValFriendship","GLOBAL",3)
    Global("T2ValCloakwood","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("T2ValCloakwood","GLOBAL",1)
    RealSetGlobalTimer("T2ValFriendTalkTimer","GLOBAL",2700)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("T2ValFriendTalkTimer","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
    !Global("T2ValFriendship","GLOBAL",3)
    Global("T2ValFriendTalk","GLOBAL",19)
    Global("T2ValCloakwood","GLOBAL",1)
THEN
  RESPONSE #100
    IncrementGlobal("T2ValFriendTalk","GLOBAL",1)
END

// FT 15
IF
    InParty(Myself)
    !Dead("t2val")
    Global("T2ValFriendTalk","GLOBAL",29)
    !Global("T2ValFriendship","GLOBAL",3)
    GlobalGT("Chapter","GLOBAL",%tutu_chapter_6%)
    Global("T2ValCandlekeep","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("T2ValCandlekeep","GLOBAL",1)
    RealSetGlobalTimer("T2ValFriendTalkTimer","GLOBAL",600)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("T2ValFriendTalkTimer","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
    !Global("T2ValFriendship","GLOBAL",3)
    Global("T2ValFriendTalk","GLOBAL",29)
    Global("T2ValCandlekeep","GLOBAL",1)
THEN
  RESPONSE #100
    IncrementGlobal("T2ValFriendTalk","GLOBAL",1)
END



//Area: %Temple_SongoftheMorning%
IF 
    InParty(Myself)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    Global("T2Val_temple","GLOBAL",0)
    AreaCheck("%Temple_SongoftheMorning%")
THEN
  RESPONSE #100
    SetGlobal("T2Val_temple","GLOBAL",1)
END

//Area: %NashkelMines_L1%
IF
    InParty(Myself)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    Global("T2Val_mines","GLOBAL",0)
    AreaCheck("%NashkelMines_L1%")
THEN
  RESPONSE #100
    SetGlobal("T2Val_mines","GLOBAL",1)
END

//Area %ValleyoftheTombs_Tomb%
IF
    InParty(Myself)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    Global("T2Val_valley","GLOBAL",0)
    AreaCheck("%ValleyoftheTombs_Tomb%")
THEN
  RESPONSE #100
    SetGlobal("T2Val_valley","GLOBAL",1)
END

//Area %CloakwoodMines_L4% sub area: shrine to Bhaal (room NW of bedroom adjacent to elevator)
/*
IF
    InParty(Myself)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    Global("T2Val_bhaal","GLOBAL",1)
    AreaCheck("%CloakwoodMines_L4%")
THEN
  RESPONSE #100
    SetGlobal("T2Val_bhaal","GLOBAL",2)
END  */

//(If you flood the mine without freeing the slaves)
IF
    InParty(Myself)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    Global("T2Val_flood","GLOBAL",0)
    !Global("FREESLAV","GLOBAL",1)
    GlobalGT("FLOODED","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("T2Val_flood","GLOBAL",1)
END

//Area %Candlekeep_Library_L1%
IF
    InParty(Myself)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    Global("T2Val_candlekeep","GLOBAL",0)
    AreaCheck("%Candlekeep_Library_L1%")
THEN
  RESPONSE #100
    SetGlobal("T2Val_candlekeep","GLOBAL",1)
END

//(When Charname finds Gorion's letter - Shadowkeeper gave me an error when looking up the item code, and Tutu triggers a dialogue when you pick up the letter, and only if Global("T2ValFriendship","GLOBAL",1))
IF
    InParty(Myself)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    Global("T2Val_letter","GLOBAL",0)
    Global("T2ValFriendship","GLOBAL",1)
    PartyHasItem("%tutu_var%SCRL2J")
THEN
  RESPONSE #100
    SetGlobal("T2Val_letter","GLOBAL",1)
END

//In inventory: POTN48.itm
IF
    InParty(Myself)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    Global("T2Val_potion","GLOBAL",0)
    PartyHasItem("POTN48")
THEN
  RESPONSE #100
    SetGlobal("T2Val_potion","GLOBAL",1)
END

//In inventory: BOOK03.itm, BOOK04.itm, BOOK05.itm, BOOK06.itm BOOK07.itm, or BOOK08.itm. Should only fire once regardless of which book you put in.
IF
    InParty(Myself)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    Global("T2Val_book","GLOBAL",0)
    OR(6)
      PartyHasItem("BOOK03")
      PartyHasItem("BOOK04")
      PartyHasItem("BOOK05")
      PartyHasItem("BOOK06")
      PartyHasItem("BOOK07")
      PartyHasItem("BOOK08")
THEN
  RESPONSE #100
    SetGlobal("T2Val_book","GLOBAL",1)
END


// Triggering talks
IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    OR(9)
      Global("T2Val_temple","GLOBAL",1)
      Global("T2Val_mines","GLOBAL",1)
      Global("T2Val_valley","GLOBAL",1)
      Global("T2Val_bhaal","GLOBAL",2)
      Global("T2Val_flood","GLOBAL",1)
      Global("T2Val_candlekeep","GLOBAL",1)
      Global("T2Val_letter","GLOBAL",1)
      Global("T2Val_potion","GLOBAL",1)
      Global("T2Val_book","GLOBAL",1)
THEN
  RESPONSE #100
    MoveToObject(Player1)
    Dialogue(Player1)
END


// Stringfixer
IF
    HotKey(K)
THEN
    RESPONSE #100
        SetName(@1001) // ~Valerie~
        SetPlayerSound(Myself,@1008,HAPPY)                  // ~Wow. We're really making a difference, aren't we?~
        SetPlayerSound(Myself,@1009,UNHAPPY_ANNOYED)        // ~I don't know if that was really necessary...~
        SetPlayerSound(Myself,@1010,UNHAPPY_SERIOUS)        // ~Ugh. You people are disgusting. I should have stayed in Amn.~
        SetPlayerSound(Myself,@1011,UNHAPPY_BREAKING_POINT) // ~Sorry, but you'll have to go on without me.~
        SetPlayerSound(Myself,@1012,LEADER)                 // ~I'm not sure I'm the best choice to lead, but I'll do my best.~
        SetPlayerSound(Myself,@1013,TIRED)                  // ~Up at all hours, no time for rest or sleep... reminds me of my apprenticeship.~
        SetPlayerSound(Myself,@1014,BORED)                  // ~Hmmm. Anyone have a good book to read?~
        SetPlayerSound(Myself,@1015,BATTLE_CRY1)            // ~In Torm's name!~
        SetPlayerSound(Myself,@1016,BATTLE_CRY2)            // ~This place shall be your grave!~
        SetPlayerSound(Myself,@1017,BATTLE_CRY3)            // ~For Amn!~
        SetPlayerSound(Myself,@1018,DYING)                  // ~I'm sorry... I couldn't...~
        SetPlayerSound(Myself,@1019,HURT)                   // ~Help! I can't stop the bleeding!~
        SetPlayerSound(Myself,@1020,AREA_FOREST)            // ~It's pretty here. Forests always seem to be home to all kinds of nasty critters, though.~
        SetPlayerSound(Myself,@1021,AREA_CITY)              // ~Kinda quaint compared to Athkatla. I think I prefer this, actually.~
        SetPlayerSound(Myself,@1022,AREA_DUNGEON)           // ~Ooooh, a dungeon... we're about to get attacked, aren't we?~
        SetPlayerSound(Myself,@1023,AREA_DAY)               // ~A nice day out. Until the fighting and the screaming start.~
        SetPlayerSound(Myself,@1024,AREA_NIGHT)             // ~This isn't good. Evil critters always seem to favor darkness.~
        SetPlayerSound(Myself,@1025,SELECT_COMMON1)         // ~I'm here.~
        SetPlayerSound(Myself,@1026,SELECT_COMMON2)         // ~Need something?~
        SetPlayerSound(Myself,@1027,SELECT_COMMON3)         // ~Need a spell?~
        SetPlayerSound(Myself,@1028,SELECT_COMMON4)         // ~Ready.~
        SetPlayerSound(Myself,@1029,SELECT_COMMON5)         // ~Yes?~
        SetPlayerSound(Myself,@1030,SELECT_COMMON6)         // ~Got a problem?~
        SetPlayerSound(Myself,@1031,SELECT_ACTION1)         // ~Alright.~
        SetPlayerSound(Myself,@1032,SELECT_ACTION2)         // ~On it.~
        SetPlayerSound(Myself,@1033,SELECT_ACTION3)         // ~Sounds easy enough.~
        SetPlayerSound(Myself,@1034,SELECT_ACTION4)         // ~I'm moving.~
        SetPlayerSound(Myself,@1035,SELECT_ACTION5)         // ~It's done.~
        SetPlayerSound(Myself,@1036,SELECT_ACTION6)         // ~Okay.~
        SetPlayerSound(Myself,@1037,SELECT_ACTION7)         // ~Let's see what the big deal is.~
        SetPlayerSound(Myself,@1038,CRITICAL_HIT)           // ~That has got to hurt.~
        SetPlayerSound(Myself,@1039,CRITICAL_MISS)          // ~Uh. Sorry?~
        SetPlayerSound(Myself,@1040,TARGET_IMMUNE)          // ~My weapon's useless! Maybe magic instead?~
        SetPlayerSound(Myself,@1041,INVENTORY_FULL)         // ~Gah. Even as an apprentice, I never had to carry this much. Sorry.~
        SetPlayerSound(Myself,@1042,PICKED_POCKET)          // ~Wonder how long they'll take to miss that.~
        SetPlayerSound(Myself,@1043,HIDDEN_IN_SHADOWS)      // ~A different kind of invisibility than I've studied.~
        SetPlayerSound(Myself,@1044,SPELL_DISRUPTED)        // ~Ah, damn it!~
        SetPlayerSound(Myself,@1045,SET_A_TRAP)             // ~It's set.~
END