BACKUP ~valerie/backup~
//AUTHOR ~Tempest (tempest@spellholdstudios.net)~
SUPPORT ~Spellhold staff at http://www.shsforums.net/forum/617-valerie/~

VERSION ~v1.2~

README ~valerie/readme/valerie-readme-english.html~

ALWAYS

	ACTION_IF NOT VARIABLE_IS_SET is_tutu BEGIN // check to make this happen only once per install

	/* -------------------------- *
	 *    Sod: Modmerge check.    *
	 * -------------------------- */
		ACTION_IF ((FILE_EXISTS ~dlc/sod-dlc.zip~) OR (FILE_EXISTS ~sod-dlc.zip~)) BEGIN FAIL @21 END


	/* -------------------------------------------------------------------------------- *
	 *    Determines which game is installed and if the game is an Enhanced Edition.    *
	 * -------------------------------------------------------------------------------- */
		OUTER_SET is_tutu  = (GAME_IS ~tutu tutu_totsc~ ? 1 : 0)                    // tutu
		OUTER_SET is_bgt   = (GAME_IS ~bgt~) ? 1 : 0                                // bgt
		OUTER_SET is_totsc = (GAME_IS ~tutu_totsc bgt bgee eet~) ? 1 : 0            // TotSC

		OUTER_SET is_bgee  = (GAME_IS ~bgee~ AND NOT GAME_INCLUDES ~sod~) ? 1 : 0   // bgee without SoD
		OUTER_SET is_sod   = (GAME_IS ~bgee~ AND GAME_INCLUDES ~sod~) ? 1 : 0       // SoD
		OUTER_SET is_eet   = (GAME_IS "eet") ? 1 : 0                                // eet
		OUTER_SET is_ee    = (GAME_IS ~bgee eet~) ? 1 : 0                           // EE game


	/* ------------------------------------------------------------------------- *
	 *    Loads Crossplatform variables according to which game is installed.    *
	 * ------------------------------------------------------------------------- */
		ACTION_IF is_tutu BEGIN
			INCLUDE ~valerie/lib/g3_tutu_cpmvars.tpa~
			PRINT @22	// ~Tutu install detected.~
		END
		ACTION_IF is_bgt BEGIN
			INCLUDE ~valerie/lib/g3_bgt_cpmvars.tpa~
			PRINT @23	// ~BGT install detected.~
		END
		ACTION_IF is_bgee OR is_sod BEGIN
			INCLUDE ~valerie/lib/liam_bgee_vars.tpa~
			PRINT @24	// ~Baldur's Gate Enhanced Edition install detected.~
		END
		ACTION_IF is_eet BEGIN
			INCLUDE ~EET/other/cpmvars/eet_cpmvars.tpa~
			PRINT @25	// ~Enhanced Edition Trilogy install detected.~
		END


	/* --------------------------------------------------------------------------------------------- *
	 *    Convert strings to UTF-8 for BGEE/BG2EE                                                    *
	 *    No exception, everything needs to be converted                                             *
	 *    setup.tra contains in game texts as well as installation texts and need to be converted    *
	 * --------------------------------------------------------------------------------------------- */
		ACTION_DEFINE_ARRAY t2#noconvert BEGIN END
		// any tra files used in LANGUAGE need to be reloaded after UTF-8 conversion
		ACTION_DEFINE_ARRAY t2#reload BEGIN setup END

		LAF HANDLE_CHARSETS
			INT_VAR
				infer_charsets = 1
			STR_VAR
				tra_path = EVAL ~%MOD_FOLDER%/lang~
				noconvert_array = t2#noconvert
				reload_array = t2#reload
		END

	END

END // of ALWAYS block


AUTO_TRA ~valerie/lang/%s~

LANGUAGE ~English~
         ~english~
         ~valerie/lang/english/setup.tra~

LANGUAGE ~Russian (Translation: JamesMaxwell & Arcanecoast.ru)~
         ~russian~
         ~valerie/lang/russian/setup.tra~


/* ================================================= *
 *  Valerie NPC Mod for BG1 Tutu, BGT BG:EE and EET  *
 * ================================================= */
BEGIN @1													 // ~Valerie NPC Mod for BG1 Tutu, BGT BG:EE and EET~
DESIGNATED 0 LABEL ~valerie_npc_for_bg~
REQUIRE_PREDICATE GAME_IS ~tutu tutu_totsc bgee eet bgt~ @2  // ~You must have Tutu, BGT, BG:EE or EET installed to install Valerie NPC for BG mod.~

INCLUDE ~%MOD_FOLDER%/lib/main_component.tpa~

