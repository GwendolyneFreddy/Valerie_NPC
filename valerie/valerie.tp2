// --------------------------------------------
// Basics
BACKUP ~valerie/backup~
AUTHOR ~Tempest (tempest@spellholdstudios.net)~
VERSION ~v1.1~
README ~valerie/valerie_readme.html~

// Crossplatform var stuff blatantly stolen from cmorgan/gavin/bg1npc!
ALWAYS
ACTION_IF FILE_EXISTS_IN_GAME ~neera.dlg~ THEN BEGIN
  /*Tell the player it is using BG:EE stuff */
  PRINT ~BG:EE install detected~
  INCLUDE ~valerie\lib\liam_bgee_vars.tpa~
END ELSE BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~FW0100.are~ THEN BEGIN
    /* Tell the player it is using Tutu stuff */
    PRINT ~Tutu install detected.~
    INCLUDE ~valerie\lib\g3_tutu_cpmvars.tpa~
  END ELSE BEGIN
    ACTION_IF FILE_EXISTS_IN_GAME ~AR7200.are~ THEN BEGIN
        /* Tell the player it is using BGT stuff */
        PRINT ~BGT install detected.~
        INCLUDE ~valerie\lib\g3_bgt_cpmvars.tpa~
        /* Tell the player it is not Tutu or BGT */
    END ELSE BEGIN FAIL ~Please install on BG:EE, Tutu or BGT.~
    END
  END
END
END

// --------------------------------------------
// Valerie NPC Mod v1.1
// --------------------------------------------
// Component setup
BEGIN ~Valerie NPC Mod for BG1 Tutu~

// Adds custom IsValidForPartyDialogue state used throughout cath
APPEND ~STATE.IDS~ // adds custom IsValidForPartyDialogue state
  ~0x80101FEF CD_STATE_NOTVALID~
  UNLESS ~CD_STATE_NOTVALID~

// Fixing Imoen's dialogue file
  /* Tutu: Giving Imoen a banter file entry in the interdi.2da */
  ACTION_IF FILE_EXISTS_IN_GAME ~FW0100.are~ THEN BEGIN
    APPEND ~interdia.2da~ ~IMOEN                    _BIMOEN~ UNLESS ~_\(BIMOEN\|bimoen\)~
  END
  /* BGT: Giving Imoen a banter file if the Fixpack isn't installed */
  ACTION_IF NOT (FILE_EXISTS_IN_GAME ~cdbehbla.pro~) AND (FILE_EXISTS_IN_GAME ~AR7200.are~) THEN BEGIN
    ACTION_IF FILE_EXISTS_IN_GAME ~saradush.mve~ THEN BEGIN
      COPY_EXISTING ~interdia.2da~ ~override~
        SET_2DA_ENTRY 17 1 2 ~BIMOEN2~ // fixes ToB version
      BUT_ONLY_IF_IT_CHANGES
    END ELSE BEGIN
        APPEND ~interdia.2da~ ~IMOEN       BIMOEN2~ // fixes SoA version
        UNLESS ~BIMOEN2~
    END
  END


    /* Imoen BIMOEN.DLG rebuild */
  ACTION_IF NOT (FILE_EXISTS_IN_GAME ~_bimoen.dlg~) AND (FILE_EXISTS_IN_GAME ~FW0100.are~) THEN BEGIN
    COMPILE ~valerie/dialogue/x#bimoen_tutu.d~
  END ELSE BEGIN
    ACTION_IF NOT (FILE_EXISTS_IN_GAME ~bimoen2.dlg~) AND (FILE_EXISTS_IN_GAME ~AR7200.are~) THEN BEGIN
      COMPILE ~valerie/dialogue/x#bimoen_bgt.d~
    END
  END

  
  /* ACTION.IDS and TRIGGER.IDS patching to ToB - thanks, Cam, if you read it */
  /* and GTIMES.IDS, ANIMATE.IDS patching to ToB, courtesy of the BG2 Fix Pack */
  INCLUDE ~valerie/lib/ids_entries_cleanup.tph~
  
/* Tutu Area Script Assignment Patching: All Areas Script ID'd */
ACTION_IF FILE_EXISTS_IN_GAME ~FW0100.are~ THEN BEGIN
  INCLUDE ~valerie/lib/tutu_area_script_assign.tph~
END

/* Area Type Flagging */
/* ToSC only: Tutu and BGT */
ACTION_IF (FILE_EXISTS_IN_GAME ~FW1500.are~) OR (FILE_EXISTS_IN_GAME ~ARW500.are~) THEN BEGIN // if TotSC is installed
  COPY_EXISTING ~%IsleofBalduranN%.are~ ~override~
                ~%IsleofBalduranS%.are~ ~override~
                ~%DurlagsTower%.are~ ~override~
                ~%Farmlands%.are~ ~override~
    READ_BYTE  "0x48" "flags"
    WRITE_BYTE "0x48" ("%flags%" BOR "0b00010001")
  BUT_ONLY_IF_IT_CHANGES
END

  /* FOREST and OUTDOOR: Tutu and BGT */
ACTION_IF (FILE_EXISTS_IN_GAME ~FW0100.are~) OR (FILE_EXISTS_IN_GAME ~AR7200.are~) THEN BEGIN
  COPY_EXISTING ~%FishingVillage%.are~ ~override~
                ~%Peldvale%.are~ ~override~
                ~%LionsWay%.are~ ~override~
                ~%CoastWay%.are~ ~override~
                ~%Larswood%.are~ ~override~
                ~%ShipwrecksCoast%.are~ ~override~
                ~%HighHedge%.are~ ~override~
                ~%MutaminsGarden%.are~ ~override~
                ~%Lighthouse%.are~ ~override~
                ~%RedCanyons%.are~ ~override~
                ~%SouthBeregostRoad%.are~ ~override~
                ~%Ulcaster%.are~ ~override~
                ~%ArchaeologicalSite%.are~ ~override~
                ~%FishermansLake%.are~ ~override~
                ~%NorthNashkelRoad%.are~ ~override~
                ~%LonelyPeaks%.are~ ~override~
                ~%FirewineBridge%.are~ ~override~
                ~%BearRiver%.are~ ~override~
                ~%ValleyoftheTombs%.are~ ~override~
                ~%DryadFalls%.are~ ~override~
                ~%FireLeafForest%.are~ ~override~
                ~%GibberlingMountains%.are~ ~override~
    READ_BYTE  "0x48" "flags"
    WRITE_BYTE "0x48" ("%flags%" BOR "0b00010001")
    BUT_ONLY_IF_IT_CHANGES

/* OUTDOOR ONLY: Tutu and BGT */
COPY_EXISTING ~%GnollStronghold%.are~ ~override~
              ~%NashkelMines%.are~ ~override~
              ~%FriendlyArmInn%.are~ ~override~
              ~%Temple%.are~ ~override~
              ~%NashkelCarnival%.are~ ~override~
  READ_BYTE  "0x48" "flags"
  WRITE_BYTE "0x48" ("%flags%" BOR "0b00000001")
  BUT_ONLY_IF_IT_CHANGES

/* CITY and OUTDOOR */
COPY_EXISTING ~%WyrmsCrossing%.are~ ~override~
              ~%Candlekeep_Ch6%.are~ ~override~
              ~%Gullykin%.are~ ~override~
  READ_BYTE  "0x48" "flags"
  WRITE_BYTE "0x48" ("%flags%" BOR "0b00001001")
  BUT_ONLY_IF_IT_CHANGES
END
  



// Compiling dialogue...
COMPILE EVALUATE_BUFFER ~valerie/dialogue/t2val.d~
COMPILE EVALUATE_BUFFER ~valerie/dialogue/bt2val.d~
COMPILE EVALUATE_BUFFER ~valerie/dialogue/t2valj.d~
COMPILE EVALUATE_BUFFER ~valerie/dialogue/t2valp.d~

// Compiling scripts...
COMPILE EVALUATE_BUFFER ~valerie/scripts/t2val.baf~
COMPILE EVALUATE_BUFFER ~valerie/scripts/t2vald.baf~

ACTION_IF FILE_EXISTS_IN_GAME ~FW0100.are~ THEN BEGIN
  EXTEND_TOP ~_AR4800.bcs~ ~valerie/scripts/fw4800.baf~
    EVALUATE_BUFFER
END ELSE BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~AR7200.are~ THEN BEGIN
    EXTEND_TOP ~AR3700.bcs~ ~valerie/scripts/fw4800.baf~
      EVALUATE_BUFFER
  END ELSE BEGIN
    ACTION_IF FILE_EXISTS_IN_GAME ~neera.dlg~ THEN BEGIN
      EXTEND_TOP ~AR4800.bcs~ ~valerie/scripts/fw4800.baf~
        EVALUATE_BUFFER
    END
  END
END



// Copying images and creatures
ACTION_IF (FILE_EXISTS_IN_GAME ~FW0100.are~) OR (FILE_EXISTS_IN_GAME ~ar7200.are~) THEN BEGIN
  COPY ~valerie/images~ ~override~
END ELSE BEGIN ACTION_IF FILE_EXISTS_IN_GAME ~neera.dlg~ THEN BEGIN
  COPY ~valerie/images/bgee~ ~override~
END
END

COPY ~valerie/creatures/t2val.cre~ ~override/t2val.cre~
  SAY NAME1 ~Valerie~
  SAY NAME2 ~Valerie~
  WRITE_ASCII 0x248 ~T2VAL~ #8  //override script
  WRITE_ASCII 0x280 ~T2VAL~ #32  //DV
  WRITE_ASCII 0x2cc ~T2VAL~ #8   //pre-joining dialogue file
  WRITE_ASCII 0X34 ~T2VALS~ #8   //small portrait
  WRITE_ASCII 0x3c ~T2VALM~ #8   //medium portrait
  SAY BIO ~Valerie is quiet when asked about her past, finding her feelings about it mixed. She was cast out of her family as a child when she displayed signs of magical talent, a deeply suspicious sign in Amn with its cultural distrust of magic. She was found and taken in by the Cowled Wizards, the nation's arcane police, who taught her how to use and control her abilities. Upon completing her apprenticeship, she was posted to Nashkel with the Amnish army where you met her.~
  SAY HAPPY ~Wow. We're really making a difference, aren't we?~ []
  SAY UNHAPPY_ANNOYED ~I don't know if that was really necessary...~ []
  SAY UNHAPPY_SERIOUS ~Ugh. You people are disgusting. I should have stayed in Amn.~ []
  SAY UNHAPPY_BREAKING_POINT ~Sorry, but you'll have to go on without me.~ []
  SAY LEADER ~I'm not sure I'm the best choice to lead, but I'll do my best.~ []
  SAY TIRED ~Up at all hours, no time for rest or sleep... reminds me of my apprenticeship.~ []
  SAY BORED ~Hmmm. Anyone have a good book to read?~ []
  SAY BATTLE_CRY1 ~In Torm's name!~ []
  SAY BATTLE_CRY2 ~This place shall be your grave!~ []
  SAY BATTLE_CRY3 ~For Amn!~ []
  SAY DYING ~I'm sorry... I couldn't...~ []
  SAY HURT ~Help! I can't stop the bleeding!~ []
  SAY AREA_FOREST ~It's pretty here. Forests always seem to be home to all kinds of nasty critters, though.~ []
  SAY AREA_CITY ~Kinda quaint compared to Athkatla. I think I prefer this, actually.~ []
  SAY AREA_DUNGEON ~Ooooh, a dungeon... we're about to get attacked, aren't we?~ []
  SAY AREA_DAY ~A nice day out. Until the fighting and the screaming start.~ []
  SAY AREA_NIGHT ~This isn't good. Evil critters always seem to favor darkness.~ []
  SAY SELECT_COMMON1 ~I'm here.~ []
  SAY SELECT_COMMON2 ~Need something?~ []
  SAY SELECT_COMMON3 ~Need a spell?~ []
  SAY SELECT_COMMON4 ~Ready.~ []
  SAY SELECT_COMMON5 ~Yes?~ []
  SAY SELECT_COMMON6 ~Got a problem?~ []
  SAY SELECT_ACTION1 ~Alright.~ []
  SAY SELECT_ACTION2 ~On it.~ []
  SAY SELECT_ACTION3 ~Sounds easy enough.~ []
  SAY SELECT_ACTION4 ~I'm moving.~ []
  SAY SELECT_ACTION5 ~It's done.~ []
  SAY SELECT_ACTION6 ~Okay.~ []
  SAY SELECT_ACTION7 ~Let's see what the big deal is.~ []
  SAY CRITICAL_HIT ~That has got to hurt.~ []
  SAY CRITICAL_MISS ~Uh. Sorry?~ []
  SAY TARGET_IMMUNE ~My weapon's useless! Maybe magic instead?~ []
  SAY INVENTORY_FULL ~Gah. Even as an apprentice, I never had to carry this much. Sorry.~ []
  SAY PICKED_POCKET ~Wonder how long they'll take to miss that.~ []
  SAY HIDDEN_IN_SHADOWS ~A different kind of invisibility than I've studied.~ []
  SAY SPELL_DISRUPTED ~Ah, damn it!~ []
  SAY SET_A_TRAP ~It's set.~ []

// Appends
APPEND ~pdialog.2da~ ~T2VAL T2VALP T2VALJ T2VALD~
  UNLESS ~T2Val~

APPEND ~interdia.2da~ ~T2VAL BT2VAL~
  UNLESS ~T2Val~