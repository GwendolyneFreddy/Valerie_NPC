/* =========================================== *
 *         MAIN COMPONENT INSTALLATION         *
 * =========================================== */


/* -------------------------------------------------------------------------------------------------- *
 *   	ADDs CamDawg's custom IsValidForPartyDialogue state used throughout isra. Thanks Cam!         *
 * -------------------------------------------------------------------------------------------------- *
 *  covers STATE_CONFUSED, STATE_FEEBLEMINDED, STATE_SILENCED, STATE_DEAD, STATE_ACID_DEATH,          *
 *  STATE_FLAME_DEATH, STATE_EXPLODING_DEATH, STATE_STONE_DEATH, STATE_FROZEN_DEATH, STATE_HELPLESS,  *
 *  STATE_STUNNED, STATE_PANIC, STATE_BERSERK, STATE_SLEEPING                                         *
 * -------------------------------------------------------------------------------------------------- */
APPEND ~state.ids~ ~0x80101FEF CD_STATE_NOTVALID~ UNLESS ~CD_STATE_NOTVALID~


/* ------------------------------ *
 *  Fixing Imoen's dialogue file  *
 * ------------------------------ */

// Tutu: Give Imoen a banter file
// ------------------------------
ACTION_IF is_tutu BEGIN
	APPEND ~interdia.2da~ ~IMOEN       _BIMOEN~ UNLESS ~_\(BIMOEN\|bimoen\)~
	COPY_EXISTING ~interdia.2da~ ~override~
		PRETTY_PRINT_2DA
END

// BGT: Add Imoen's banter file if Fixpack isn't installed
// -------------------------------------------------------
ACTION_IF NOT FILE_EXISTS_IN_GAME ~cdbehbla.pro~ AND (is_bgt) BEGIN
	COPY_EXISTING ~interdia.2da~ ~override~
		SET_2DA_ENTRY 17 1 2 ~BIMOEN2~ // fixes ToB version
	BUT_ONLY
	COPY_EXISTING ~interdia.2da~ ~override~
		PRETTY_PRINT_2DA
END

// Gives Imoen a banter file if there isn't one already
// ----------------------------------------------------
ACTION_IF (is_tutu) AND NOT (FILE_EXISTS_IN_GAME ~_bimoen.dlg~) BEGIN
	<<<<<<<< valerie/dialogue/_bimoen.d
BEGIN _BIMOEN
  >>>>>>>>
	COMPILE ~valerie/dialogue/_bimoen.d~
END
ACTION_IF (is_bgt) AND NOT (FILE_EXISTS_IN_GAME ~bimoen2.dlg~) BEGIN
	<<<<<<<< valerie/dialogue/bimoen2.d
BEGIN BIMOEN2
  >>>>>>>>
	COMPILE ~valerie/dialogue/bimoen2.d~
END


/* ------------------------------------------ *
 *  Fixing .ids, courtesy of the BG2 Fixpack  *
 * ------------------------------------------ */

// Adds ToB trigger/actions to SoA
// -------------------------------
INCLUDE ~valerie/lib/tob2soa.tph~

// Adds timers entry in gtimes.ids
// -------------------------------
INCLUDE ~valerie/lib/ids_entries_gtimes.tph~


/* ------------------------------------------ *
 *  Areas patching                            *
 * ------------------------------------------ *
 *  - Tutu: Assignment a script to all Areas  *
 *  - Area Type Flagging:                     *
 *        - bit 0: Outdoor                    *
 *        - bit 3: City                       *
 *        - bit 4: Forest                     *
 * ------------------------------------------ */

// Tutu Area Script Assignment Patching: All Areas Script ID'd
// -----------------------------------------------------------
ACTION_IF is_tutu BEGIN
	INCLUDE ~valerie/lib/tutu_area_script_assign.tph~
END

// TotSC content
// -------------
ACTION_IF (is_totsc) THEN BEGIN // if TotSC is installed (tutu, bgt and bg:ee)
	COPY_EXISTING ~%IsleofBalduranN%.are~ ~override~
                  ~%IsleofBalduranS%.are~ ~override~
                  ~%DurlagsTower%.are~ ~override~
                  ~%Farmlands%.are~ ~override~
		WRITE_BYTE 0x48 (THIS | (BIT0 | BIT4))	// FOREST and OUTDOOR (THIS BOR "0b00010001")
	BUT_ONLY
END

// BG content
// ----------
ACTION_IF (is_tutu) OR (is_bgt) OR (is_ee) THEN BEGIN
	COPY_EXISTING ~%FishingVillage%.are~ ~override~
                  ~%Peldvale%.are~ ~override~
                  ~%LionsWay%.are~ ~override~
                  ~%CoastWay%.are~ ~override~
                  ~%Larswood%.are~ ~override~
                  ~%ShipwrecksCoast%.are~ ~override~
                  ~%HighHedge%.are~ ~override~
                  ~%MutaminsGarden%.are~ ~override~
                  ~%Lighthouse%.are~ ~override~
                  ~%RedCanyons%.are~ ~override~
                  ~%SouthBeregostRoad%.are~ ~override~
                  ~%Ulcaster%.are~ ~override~
                  ~%ArchaeologicalSite%.are~ ~override~
                  ~%FishermansLake%.are~ ~override~
                  ~%NorthNashkelRoad%.are~ ~override~
                  ~%LonelyPeaks%.are~ ~override~
                  ~%FirewineBridge%.are~ ~override~
                  ~%BearRiver%.are~ ~override~
                  ~%ValleyoftheTombs%.are~ ~override~
                  ~%DryadFalls%.are~ ~override~
                  ~%FireLeafForest%.are~ ~override~
                  ~%GibberlingMountains%.are~ ~override~
		WRITE_BYTE 0x48 (THIS | (BIT0 | BIT4))	// FOREST and OUTDOOR (THIS BOR "0b00010001")
    BUT_ONLY

	COPY_EXISTING ~%GnollStronghold%.are~ ~override~
                  ~%NashkelMines%.are~ ~override~
                  ~%FriendlyArmInn%.are~ ~override~
                  ~%Temple%.are~ ~override~
                  ~%NashkelCarnival%.are~ ~override~
		WRITE_BYTE 0x48 (THIS | BIT0)	// OUTDOOR ONLY (THIS BOR "0b00000001")
	BUT_ONLY

	COPY_EXISTING ~%WyrmsCrossing%.are~ ~override~
                  ~%Candlekeep_Ch6%.are~ ~override~
                  ~%Gullykin%.are~ ~override~
		WRITE_BYTE 0x48 (THIS | (BIT0 | BIT3))	// CITY and OUTDOOR (THIS BOR "0b00001001")
	BUT_ONLY
END


/* ---------------------------------- *
 *  Installing dialogues and scripts  *
 * ---------------------------------- */

// Compiling dialogues...
COMPILE ~valerie/dialogue/t2val.d~
COMPILE EVALUATE_BUFFER ~valerie/dialogue/bt2val.d~
COMPILE EVALUATE_BUFFER ~valerie/dialogue/t2valj.d~
COMPILE EVALUATE_BUFFER ~valerie/dialogue/t2valp.d~

// Compiling scripts...
COMPILE EVALUATE_BUFFER ~valerie/scripts/t2val.baf~
COMPILE EVALUATE_BUFFER ~valerie/scripts/t2vald.baf~

COMPILE EVALUATE_BUFFER ~isra/scripts/rh#isfix.baf~	USING ~isra/translations/%s\setup.tra~

EXTEND_TOP ~%NashkelMines_BCS%.bcs~ ~isra/scripts/fw5400.baf~ EVALUATE_BUFFER
ACTION_IF FILE_EXISTS_IN_GAME ~FW0100.are~ THEN BEGIN
  EXTEND_TOP ~_AR4800.bcs~ ~valerie/scripts/fw4800.baf~
    EVALUATE_BUFFER
END ELSE BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~AR7200.are~ THEN BEGIN
    EXTEND_TOP ~AR3700.bcs~ ~valerie/scripts/fw4800.baf~
      EVALUATE_BUFFER
  END ELSE BEGIN
    ACTION_IF FILE_EXISTS_IN_GAME ~neera.dlg~ THEN BEGIN
      EXTEND_TOP ~AR4800.bcs~ ~valerie/scripts/fw4800.baf~
        EVALUATE_BUFFER
    END
  END
END

// SoD/BGT compatibility: Isra leaves the party at transition.
ACTION_IF (is_sod) OR (is_eet) BEGIN
	EXTEND_TOP ~bd0103.bcs~ ~isra/scripts/bd0103.baf~
END ELSE BEGIN
	ACTION_IF (is_bgt) BEGIN
		EXTEND_TOP ~aram00.bcs~ ~isra/scripts/aram00.baf~
	END
END


/* ---------------------- *
 *  Installing portraits  *
 * ---------------------- */
ACTION_IF is_ee BEGIN
	COPY ~isra/portraits/rh#isral.bmp~ ~override~
	COPY ~isra/portraits/rh#isral.bmp~ ~override/rh#isram.bmp~
END ELSE BEGIN
	COPY ~isra/portraits~ ~override~
END
// Copying images and creatures
ACTION_IF (FILE_EXISTS_IN_GAME ~FW0100.are~) OR (FILE_EXISTS_IN_GAME ~ar7200.are~) THEN BEGIN
  COPY ~valerie/images~ ~override~
END ELSE BEGIN
 ACTION_IF FILE_EXISTS_IN_GAME ~neera.dlg~ THEN BEGIN
  COPY ~valerie/images/bgee~ ~override~
 END
END



/* ---------------------- *
 *  Installing creatures  *
 * ---------------------- */

// Valerie
COPY ~valerie/creatures/t2val.cre~ ~override~
	SAY NAME1 @1001 // ~Valerie~
	SAY NAME2 @1001
	WRITE_ASCII 0x248 ~t2val~ #8  // override script
	WRITE_ASCII 0x280 ~t2val~ #32 // DV
	WRITE_ASCII 0x2cc ~t2val~ #8  // pre-joining dialogue file
	PATCH_IF is_ee BEGIN
		WRITE_ASCII 0x34 ~rh#isram~ #8   // small portrait
		WRITE_ASCII 0x3c ~rh#isral~ #8   // medium portrait
	END ELSE BEGIN
		WRITE_ASCII 0x34 ~t2vals~ #8   // small portrait
		WRITE_ASCII 0x3c ~t2valm~ #8   // medium portrait
	END
	SAY BIO @1002 // ~Valerie is quiet when asked about her past, finding her feelings about it mixed. She was cast out of her family as a child when she displayed signs of magical talent, a deeply suspicious sign in Amn with its cultural distrust of magic. She was found and taken in by the Cowled Wizards, the nation's arcane police, who taught her how to use and control her abilities. Upon completing her apprenticeship, she was posted to Nashkel with the Amnish army where you met her.~
	SAY MORALE ~~
	SAY HAPPY @1008						// ~Wow. We're really making a difference, aren't we?~
	SAY UNHAPPY_ANNOYED @1009			// ~I don't know if that was really necessary...~
	SAY UNHAPPY_SERIOUS @1010			// ~Ugh. You people are disgusting. I should have stayed in Amn.~
	SAY UNHAPPY_BREAKING_POINT @1011	// ~Sorry, but you'll have to go on without me.~
	SAY LEADER @1012					// ~I'm not sure I'm the best choice to lead, but I'll do my best.~
	SAY TIRED @1013						// ~Up at all hours, no time for rest or sleep... reminds me of my apprenticeship.~
	SAY BORED @1014						// ~Hmmm. Anyone have a good book to read?~
	SAY BATTLE_CRY1 @1015				// ~In Torm's name!~
	SAY BATTLE_CRY2 @1016				// ~This place shall be your grave!~
	SAY BATTLE_CRY3 @1017				// ~For Amn!~
	SAY DYING @1018						// ~I'm sorry... I couldn't...~
	SAY HURT @1019						// ~Help! I can't stop the bleeding!~
	SAY AREA_FOREST @1020				// ~It's pretty here. Forests always seem to be home to all kinds of nasty critters, though.~
	SAY AREA_CITY @1021					// ~Kinda quaint compared to Athkatla. I think I prefer this, actually.~
	SAY AREA_DUNGEON @1022				// ~Ooooh, a dungeon... we're about to get attacked, aren't we?~
	SAY AREA_DAY @1023					// ~A nice day out. Until the fighting and the screaming start.~
	SAY AREA_NIGHT @1024				// ~This isn't good. Evil critters always seem to favor darkness.~
	SAY SELECT_COMMON1 @1025			// ~I'm here.~
	SAY SELECT_COMMON2 @1026			// ~Need something?~
	SAY SELECT_COMMON3 @1037			// ~Need a spell?~
	SAY SELECT_COMMON4 @1038			// ~Ready.~
	SAY SELECT_COMMON5 @1029			// ~Yes?~
	SAY SELECT_COMMON6 @1030			// ~Got a problem?~
	SAY SELECT_ACTION1 @1031			// ~Alright.~
	SAY SELECT_ACTION2 @1032			// ~On it.~
	SAY SELECT_ACTION3 @1033			// ~Sounds easy enough.~
	SAY SELECT_ACTION4 @1034			// ~I'm moving.~
	SAY SELECT_ACTION5 @1035			// ~It's done.~
	SAY SELECT_ACTION6 @1036			// ~Okay.~
	SAY SELECT_ACTION7 @1037			// ~Let's see what the big deal is.~
	SAY CRITICAL_HIT @1038				// ~That has got to hurt.~
	SAY CRITICAL_MISS @1039				// ~Uh. Sorry?~
	SAY TARGET_IMMUNE @1040				// ~My weapon's useless! Maybe magic instead?~
	SAY INVENTORY_FULL @1041			// ~Gah. Even as an apprentice, I never had to carry this much. Sorry.~
	SAY PICKED_POCKET @1042				// ~Wonder how long they'll take to miss that.~
	SAY HIDDEN_IN_SHADOWS@1043			// ~A different kind of invisibility than I've studied.~
	SAY SPELL_DISRUPTED @1044			// ~Ah, damn it!~
	SAY SET_A_TRAP @1045				// ~It's set.~
BUT_ONLY


/* ----------------- *
 *  Appending files  *
 * ----------------- */
APPEND ~pdialog.2da~ ~T2VAL T2VALP T2VALJ T2VALD~ UNLESS ~T2Val~

APPEND ~interdia.2da~ ~T2VAL BT2VAL~ UNLESS ~T2Val~
